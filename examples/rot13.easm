#include "./examples/natives.hasm"

; only this string needs to be changed
#const secret "Uryyb, jbeyq! Sebz EBG13."

; this small hack will take care of secret_end of string
#const secret_end ""

#const ROT13 13
#const MOD   26

; high >= value >= low
is_between:
	swap 3
	swap 1
	dup 1
	gei
	swap 2
	gei
	andb
	swap 1
	ret

rot13:
	swap 2
	dup 1
	minusi
	push ROT13
	plusi
	push MOD
	modi
	plusi
	swap 1
	ret

#entry main
main:
    	push secret

loop:
	dup 0
	read8

	upper_case:
		dup 0
		push 'A'
		push 'Z'
		call is_between
		not
		jmp_if lower_case

		push 'A'      ; lower bound
		call rot13

		dup 1
		swap 1
		write8

		jmp inc

	lower_case:
		dup 0
		push 'a'
		push 'z'
		call is_between
		not
		jmp_if not_a_rot_char

		push 'a'      ; lower bound
		call rot13

		dup 1
		swap 1
		write8

		jmp inc

	not_a_rot_char:
		drop    ; drop the current character on the stack as it's not supported
			; by ROT13, effectively its state in memory is unaltered

		inc:
		push 1
		plusi

		dup 0
		push secret_end
		eqi
		not
		jmp_if loop

print:
	push secret_end
	push 10
	write8

	push secret
	push 1
	push secret_end
	plusi
	push secret
    	minusi
	native write

	halt
